#!/usr/bin/env coffee

async      = require 'async'
fs         = require 'fs-plus'
Handlebars = require 'handlebars'
Humanize   = require 'humanize-plus'
path       = require 'path'
Progress   = require 'progress'

Handlebars.registerHelper 'cash_graph',   require '../src/cash-graph'
Handlebars.registerHelper 'eps_graph',    require '../src/eps-graph'
Handlebars.registerHelper 'profit_graph', require '../src/profit-graph'

companyJsonFiles = fs.listSync(path.resolve(__dirname, '..', 'company'), ['.json'])
companies = []

readCompany = (symbolPath, callback) ->
  fs.readFile symbolPath, (error, contents) ->
    return callback(error) if error?

    symbol = path.basename(symbolPath, path.extname(symbolPath))
    company = JSON.parse(contents)
    company.humanizedName = company.name?.replace(/,?\s+(inc\.?|company|corp(oration)?|group)\s*$/i, '')
    companies.push(company)
    callback()

queue = async.queue(readCompany)
queue.push(companyJsonFile) for companyJsonFile in companyJsonFiles
queue.concurrency = 100
queue.drain = ->
  indexTemplate = Handlebars.compile(fs.readFileSync(path.resolve(__dirname, '..', 'index.hbs'), 'utf8'))
  fs.writeFileSync(path.resolve(__dirname, '..', 'index.html'), indexTemplate({companies}))
