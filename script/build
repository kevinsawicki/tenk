#!/usr/bin/env coffee

fs         = require 'fs-plus'
Handlebars = require 'handlebars'
path       = require 'path'

Handlebars.registerHelper 'google_map', ->
  url = "https://www.google.com/maps/embed/v1/place"
  key = 'AIzaSyD4GWPKpSy_j68tCxxTjIhtxURnRwmdCdw'
  zoom = 10
  q = @address.street1
  q += " #{@address.street2}" if @address.street2
  q += " #{@address.city}"
  q += " #{@address.state}"
  q += " #{@address.zip}" if @address.zip
  q = encodeURIComponent(q)

  new Handlebars.SafeString """
    <iframe frameborder="0" class="google-map" src="#{url}?q=#{q}&key=#{key}&zoom=#{zoom}"></iframe>
  """

template = Handlebars.compile(fs.readFileSync(path.resolve(__dirname, '..', 'company', 'company.hbs'), 'utf8'))

buildPage = (symbolPath) ->
  symbol = path.basename(symbolPath, path.extname(symbolPath))
  company = JSON.parse(fs.readFileSync(symbolPath, 'utf8'))
  fs.writeFileSync(path.resolve(__dirname, '..', 'company', "#{symbol}.html"), template(company))

fs.listSync(path.resolve(__dirname, '..', 'company'), ['.json']).forEach(buildPage)
