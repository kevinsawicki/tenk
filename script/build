#!/usr/bin/env coffee

async      = require 'async'
fs         = require 'fs-plus'
Handlebars = require 'handlebars'
Humanize   = require 'humanize-plus'
path       = require 'path'
Progress   = require 'progress'

Handlebars.registerHelper 'cash_graph',   require '../src/cash-graph'
Handlebars.registerHelper 'eps_graph',    require '../src/eps-graph'
Handlebars.registerHelper 'profit_graph', require '../src/profit-graph'

companyJsonFiles = fs.listSync(path.resolve(__dirname, '..', 'company'), ['.json'])
companies = []

readCompany = (symbolPath, callback) ->
  fs.readFile symbolPath, (error, contents) ->
    return callback(error) if error?

    symbol = path.basename(symbolPath, path.extname(symbolPath))
    company = JSON.parse(contents)
    company.humanizedName = company.name.replace(/[\s,]*(inc(\.|orporated)?|company|corp(oration)?|co\.)([\s,]*|$)/gi, '')
    company.humanizedName = company.humanizedName.replace(/\(.*\)/gi, '')
    company.humanizedName = company.humanizedName.replace(/(,|\sand|\s&)\s*$/gi, '')
    company.humanizedName = company.humanizedName.replace(/^the\s+/gi, '')
    company.humanizedName = company.humanizedName.trim()
    companies.push(company)
    callback()

queue = async.queue(readCompany)
queue.push(companyJsonFile) for companyJsonFile in companyJsonFiles
queue.concurrency = 100
queue.drain = ->
  companies.sort (company1, company2) ->
    if company1.profit[2013]? and company2.profit[2013]?
      difference = company2.profit[2013] - company1.profit[2013]
      return difference unless difference is 0
    company1.name.localeCompare(company2.name)
  companies.forEach (company, index) -> company.ranking = index + 1
  indexTemplate = Handlebars.compile(fs.readFileSync(path.resolve(__dirname, '..', 'index.hbs'), 'utf8'))
  fs.writeFileSync(path.resolve(__dirname, '..', 'index.html'), indexTemplate({companies}))
